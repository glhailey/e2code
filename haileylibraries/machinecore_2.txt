if(first()|duped()|dupefinished())
{
    #include "haileylibraries/delaydo"

    function transferAlongPath_delay(Input:table){
        Components = Input["components",table]
        ComponentA = Input["component_a",table]
        ComponentB = Input["component_b",table]
        Item = Input["item",table]

        switch(Input["STATE",string]){
            case "exit", default,
            if(ComponentA:exists("exitItem"))
            {
                NearestExit = ComponentA["nearestExit",string]
                NearestEntrance = ComponentB["nearestEntrance",string]

                # Calculate exit position
                Exit = NearestExit(ComponentA, Item["itemPosition",string](Item)[vector])[vector]
                Entrance = NearestEntrance(ComponentB, Exit)[vector]
                for(I=1, 3){
                    Exit = NearestExit(ComponentA, Entrance)[vector]
                    Entrance = NearestEntrance(ComponentB, Exit)[vector]
                }

                Input["STATE",string] = "enter"
                ComponentA["exitItem",string](Item, ComponentA, Exit, delayDoTable("transferAlongPath_delay", Input, 10))
                break
            }

            case "enter",
                Entrance = ComponentB["nearestEntrance",string](ComponentB,Item["itemPosition",string](Item)[vector])[vector]

                Input["STATE",string] = "handle"

                ComponentB["enterItem",string](Item, ComponentB, Entrance, delayDoTable("transferAlongPath_delay", Input, 10))
            break

            case "handle",
                if(ComponentA:exists("itemLeft")){
                    ComponentA["itemLeft", string](Item, ComponentA)
                }

                if(Components:count()){
                    local Callback = delayDoTable("transferAlongPath_delay", Input, 10)

                    Input["component_a",table] = ComponentB
                    Input["component_b",table] = Components:removeTable(1)
                    Input["STATE",string] = "exit"
                    if(ComponentB:exists("handleItem")){
                        ComponentB["handleItem",string](Item, ComponentB, Callback)
                    }else {
                        delayDo(Callback)
                    }
                } else {
                    if(ComponentB:exists("handleItem")){
                        ComponentB["handleItem",string](Item, ComponentB, Input["callback",table])
                    }else {
                        delayDo(Input["callback",table])
                    }
                }
            break
        }
    }

    function transferItemAlongPath(Item:table, Components:table, Callback:table){
        local Input = table()
        if(Item:exists("current_component")){
            Input = table(
                "item" = Item,
                "callback" = Callback,
                "component_a" = Item["current_component",table],
                "component_b" =  Components[1,table],
                "components" = Components,
                "STATE" = "exit"
            )
        } else {
            ComponentB = Components:removeTable(1)
            Input = table(
                "item" = Item,
                "callback" = Callback,
                "component_a" = table(),
                "component_b" =  ComponentB,
                "components" = Components,
                "STATE" = "enter"
            )
        }

        transferAlongPath_delay(Input)
    }


    function transferItem(Item:table, Component:table, Callback:table){
        transferItemAlongPath(Item, table(1=Component), Callback)
    }
}
